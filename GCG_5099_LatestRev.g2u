Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Author Bix
'Date 6/22/2018
'Customer Harbor Manufacturing
'GCG_5099_LatestRev.g2u
'On specific screens, when the part number is entered manually and the text box has lost its focus, the rev field will be automatically populated with the latest revision according to customer's revision system
'Customer wanted this to work for Parent on BOM header, adding BOM lines via append line, and Physical Inventory but there aren't hooks for these to work
V.Local..BulkDeclareString(sError,sPart,sRouter)
F.Intrinsic.Control.Try

'Get PartNo from screen
F.Intrinsic.Control.SelectCase(V.Caller.Hook)
	F.Intrinsic.Control.Case(10130)
		'Check if Inventory Screen is NEW
		F.Intrinsic.Control.If(V.Caller.Switches,=,"N")
			'End if NEW
			F.Intrinsic.Control.End
		F.Intrinsic.Control.EndIf
		'InventoryHeader PartChg
		V.Local.sPart.Set(V.Passed.000007)
	F.Intrinsic.Control.Case(10290)
		'QuoteLines PartChg
		V.Local.sPart.Set(V.Passed.000052)
	F.Intrinsic.Control.Case(11020)
		'Check if Router Screen is NEW
		F.Intrinsic.Control.If(V.Caller.Switches,=,"N")
			'End if NEW
			F.Intrinsic.Control.End
		F.Intrinsic.Control.EndIf
		'RouterHeader RtrChg
		V.Local.sRouter.Set(V.Passed.000014)
	F.Intrinsic.Control.Case(11240)
		'RouterLines PartChg
		V.Local.sPart.Set(V.Passed.000112)
	F.Intrinsic.Control.Case(12040)
		'SOLines PartChg
		V.Local.sPart.Set(V.Passed.000052)
	F.Intrinsic.Control.Case(14051)
		'Supply&Demand w/Edit PartChg
		V.Local.sPart.Set(V.Passed.000003)
	F.Intrinsic.Control.Case(14610)
		'Standalone Iss/Recp PartChg
		V.Local.sPart.Set(V.Passed.000003)
	F.Intrinsic.Control.Case(16661)
		'WorkOrder PartChg
		V.Local.sPart.Set(V.Passed.000012)
	F.Intrinsic.Control.Case(17181)
		'POLines PartChg
		V.Local.sPart.Set(V.Passed.000008)
	F.Intrinsic.Control.Case(28020)
		'InventoryTransfers PartChg
		V.Local.sPart.Set(V.Passed.000005)
F.Intrinsic.Control.EndSelect

'Pass PartNo to get Rev in subroutine (returns as V.Args.sRev)
F.Intrinsic.Control.If(V.Local.sPart,!=,"")
	F.Intrinsic.Control.CallSub(QueryRev,"sPart",V.Local.sPart)

	'Set Rev in screen
	F.Intrinsic.Control.SelectCase(V.Caller.Hook)
		F.Intrinsic.Control.Case(10130)
			'InventoryHeader PartChg
			V.Passed.000008.Set(V.Args.sRev)
		F.Intrinsic.Control.Case(10290)
			'QuoteLines PartChg
			V.Passed.000053.Set(V.Args.sRev)
		F.Intrinsic.Control.Case(11240)
			'RouterLines PartChg
			V.Passed.000133.Set(V.Args.sRev)
		F.Intrinsic.Control.Case(12040)
			'SOLines PartChg
			V.Passed.000053.Set(V.Args.sRev)
		F.Intrinsic.Control.Case(14051)
			'Supply&Demand w/Edit PartChg
			V.Passed.000099.Set(V.Args.sRev)
		F.Intrinsic.Control.Case(14610)
			'Standalone Iss/Recp PartChg
			V.Passed.000004.Set(V.Args.sRev)
		F.Intrinsic.Control.Case(16661)
			'WorkOrder PartChg
			V.Passed.000013.Set(V.Args.sRev)
		F.Intrinsic.Control.Case(17181)
			'POLines PartChg
			V.Passed.000009.Set(V.Args.sRev)
		F.Intrinsic.Control.Case(28020)
			'InventoryTransfers PartChg
			V.Passed.000006.Set(V.Args.sRev)
	F.Intrinsic.Control.EndSelect
F.Intrinsic.Control.EndIf
'Pass RouterNo to get Rev in subroutine (retuns as V.Args.sRev)
F.Intrinsic.Control.If(V.Local.sRouter,!=,"")
	F.Intrinsic.Control.CallSub(QueryRevRouter,"sRouter",V.Local.sRouter)
	
	'Set Rev in screen
	F.Intrinsic.Control.SelectCase(V.Caller.Hook)
		F.Intrinsic.Control.Case(11020)
			'Router Header RtrChg
			V.Passed.000015.Set(V.Args.sRev)
	F.Intrinsic.Control.EndSelect
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.QueryRev.Start
'Uses passed V.Args.sPart variable to query DB for latest revision of the part
V.Local..BulkDeclareString(sError,sRev)
F.Intrinsic.Control.Try

F.Intrinsic.String.Build("select top 1 right(PART, 3) as REV from V_INVENTORY_MSTR where left(PART, 17) = '{0}' order by REV desc;",V.Args.sPart,V.Local.sRev)
F.ODBC.Connection!con.OpenCompanyConnection
F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sRev,V.Local.sRev)
F.ODBC.Connection!con.Close

'Add the return variable for V.Args.sRev
F.Intrinsic.Variable.AddRV("sRev",V.Local.sRev)

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {3}->{2}{1}Error Occurred {4} with description {5}{1}{1}GAB Version: {6}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Args.sOtherSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
F.Intrinsic.Control.EndTry
Program.Sub.QueryRev.End

Program.Sub.QueryRevRouter.Start
'Uses passed V.Args.sRouter variable to query DB for latest revision of the part
V.Local..BulkDeclareString(sError,sRev)
F.Intrinsic.Control.Try

F.Intrinsic.String.Build("select top 1 right(ROUTER, 3) as REV from V_ROUTER_HEADER where left(ROUTER, 17) = '{0}' order by REV desc;",V.Args.sRouter,V.Local.sRev)
F.ODBC.Connection!con.OpenCompanyConnection
F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sRev,V.Local.sRev)
F.ODBC.Connection!con.Close

'Add the return variable for V.Args.sRev
F.Intrinsic.Variable.AddRV("sRev",V.Local.sRev)

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {3}->{2}{1}Error Occurred {4} with description {5}{1}{1}GAB Version: {6}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Args.sOtherSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
F.Intrinsic.Control.EndTry
Program.Sub.QueryRevRouter.End
